resource "oci_core_instance" "oci_04" {
  display_name        = "oci-04"
  availability_domain = "WMjr:EU-FRANKFURT-1-AD-1"
  compartment_id      = var.oci_compartment_id

  shape = "VM.Standard.A1.Flex"
  shape_config {
    ocpus         = 1
    memory_in_gbs = 6
  }

  source_details {
    source_type = "image"
    source_id   = data.oci_core_image.ubuntu-2204-minimal-aarch64-202402180.image_id
  }

  create_vnic_details {
    subnet_id = oci_core_subnet.oci_subnet_01.id
  }

  metadata = {
    ssh_authorized_keys = var.ssh_public_key
  }
}

resource "null_resource" "disko-disk-id-oci-04" {
  depends_on = [oci_core_instance.oci_04]

  provisioner "local-exec" {
    command = "${path.module}/scripts/disko-disk-path.sh"
    environment = {
      TARGET_HOST = "oci-04"
      REMOTE_HOST = oci_core_instance.oci_04.public_ip
      REMOTE_USER = var.nixos_anywhere_ssh_user
      DISK_PATH_CRITERIA = "wwn"
    }
  }
}

module "nix-oci-04" {
  depends_on             = [null_resource.disko-disk-id-oci-04]
  source                 = "github.com/numtide/nixos-anywhere//terraform/all-in-one"
  nixos_system_attr      = "..#nixosConfigurations.oci-04.config.system.build.toplevel"
  nixos_partitioner_attr = "..#nixosConfigurations.oci-04.config.system.build.diskoScript"
  target_host            = oci_core_instance.oci_04.public_ip
  install_user           = var.nixos_anywhere_ssh_user
  instance_id            = oci_core_instance.oci_04.id
  debug_logging          = true

  extra_environment = {
    TARGET_HOST = "oci-04"
  }
  disk_encryption_key_scripts = [
    {
      path   = "/tmp/disk-1.key",
      script = "${path.module}/scripts/decrypt-luks-passphrase.sh"
    }
  ]
  extra_files_script = "${path.module}/scripts/decrypt-ssh-secrets.sh"
}

# vim: set ft=terraform :
